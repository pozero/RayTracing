#version 460

#extension GL_EXT_scalar_block_layout: require

layout(constant_id = 0) const uint SPHERE_COUNT = 1;

layout(constant_id = 1) const uint GRID_ALONG_X_AXIS = 1;

layout(constant_id = 2) const uint GRID_ALONG_Y_AXIS = 1;

layout(constant_id = 3) const uint GRID_ALONG_Z_AXIS = 1;

// layout(constant_id = 4) const uint GRID_SPHERE_PAIR_ROUNDED_UP_COUNT = 1;

#include "aabb.glsl"
#include "material.glsl"
#include "geometry.glsl"
#include "random.glsl"
#include "uniform_grid.glsl"

#define RAY_BOUNCE_MAX_COUNT 10

struct ray_t {
    vec3 origin;
    vec3 direction;
};

struct hit_record_t {
    vec3 point;
    // normal always points against ray direction
    vec3 normal;
    material_t material;
    float t;
    bool front_face;
};

vec3 ray_at(const in ray_t ray,
            const in float t);

hit_record_t empty_hit_record();

void hit_record_set_front_face(inout hit_record_t record, 
                               const in ray_t ray,
                               const in vec3 outward_normal);

bool hit_aabb(const in aabb_t aabb,
              const in ray_t ray,
              inout float t_min,
              inout float t_max);

bool hit_sphere(const in sphere_t sphere, 
                const in ray_t ray, 
                const in float t_min,
                const in float t_max,
                out hit_record_t record);

vec3 gradient_background(const in vec3 top_color,
                         const in vec3 bottom_color,
                         const in ray_t ray);

bool near_zero(const in vec3 vector);

float reflectance(const in float cosine, 
                  const in float refraction_ratio);

vec3 scatter(inout ray_t ray,
             const in hit_record_t hit_record);

vec3 brute_force_ray_tracing(in ray_t ray);

vec3 ddd_dda_ray_tracing(in ray_t ray);

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(rgba32f, binding = 0) uniform image2D img_output;

layout(std430, binding = 0) uniform camera {
    vec3 pixel_delta_u;
    vec3 pixel_delta_v;
    vec3 upper_left_pixel;
    vec3 camera_position;
    float accumulated_scalar;
};

layout(std430, binding = 1) uniform sphere_geometries {
    sphere_t spheres[SPHERE_COUNT];
};

layout(std430, binding = 2) uniform acceleration_structure {
    uniform_grid_t uniform_grid;
    aabb_t world_aabb;
};

layout(std430, binding = 3) readonly buffer intersection_pair_geometry {
    uint intersection_sphere_id[];
};

layout(std430, binding = 5) readonly buffer geometry_in_grid {
    uint geometry_in_grid_start[GRID_ALONG_X_AXIS * GRID_ALONG_Y_AXIS * GRID_ALONG_Z_AXIS + 1];
};

void main() {
    const ivec2 tex_coord = ivec2(gl_GlobalInvocationID.xy);
    const vec3 pixel = upper_left_pixel + 
                 tex_coord.x * pixel_delta_u + 
                 tex_coord.y * pixel_delta_v;

    const float sample_offset_x = -0.5 + rand_01(vec2(gl_GlobalInvocationID.x, accumulated_scalar));
    const float sample_offset_y = -0.5 + rand_01(vec2(gl_GlobalInvocationID.y, 1.0f - accumulated_scalar));
    const vec3 pixel_sample_offset = sample_offset_x * pixel_delta_u + 
                                     sample_offset_y * pixel_delta_v;
    const vec3 pixel_sample = pixel + pixel_sample_offset;
    ray_t ray = ray_t(camera_position, normalize(pixel_sample - camera_position));
    vec3 destination_color = ddd_dda_ray_tracing(ray);

    const vec3 source_color = imageLoad(img_output, tex_coord).xyz;
    imageStore(img_output, 
               tex_coord, 
               vec4(source_color + destination_color, 1.0));
}

vec3 ray_at(const in ray_t ray,
            const in float t) {
    return ray.origin + t * ray.direction;
}

hit_record_t empty_hit_record() {
    return hit_record_t(
        vec3(0.0, 0.0, 0.0),
        vec3(0.0, 0.0, 0.0),
        empty_material(),
        0.0,
        false
    );
}

void hit_record_set_front_face(inout hit_record_t record, 
                               const in ray_t ray,
                               const in vec3 outward_normal) {
    record.front_face = dot(ray.direction, outward_normal) < 0.0;
    record.normal = record.front_face ? outward_normal : -outward_normal;
}

bool hit_aabb(const in aabb_t aabb,
              const in ray_t ray,
              inout float t_min,
              inout float t_max) {
    for (int a = 0; a < 3; ++ a) {
        const float inv_d = 1.0 / ray.direction[a];
        const int negative_inv_d = int(inv_d < 0.0);
        const float orig = ray.origin[a];
        const vec2 interval = get_aabb_interval(aabb, a);
        const float t0 = (interval[negative_inv_d] - orig) * inv_d;
        const float t1 = (interval[1 - negative_inv_d] - orig) * inv_d;
        t_min = max(t0, t_min);
        t_max = min(t1, t_max);
    }
    return t_min <= t_max;
}

bool hit_sphere(const in sphere_t sphere, 
                const in ray_t ray, 
                const in float t_min,
                const in float t_max,
                out hit_record_t record) {
    const vec3 oc = ray.origin - sphere.center;
    const float a = dot(ray.direction, ray.direction);
    const float half_b = dot(oc, ray.direction);
    const float c = dot(oc, oc) - sphere.radius * sphere.radius;
    const float discriminant = half_b * half_b - a * c;
    if (discriminant < 0.0) {
        return false;
    } else {
        float t = (-half_b - sqrt(discriminant)) / a;
        if (t < t_min || t > t_max) {
            t = (-half_b + sqrt(discriminant)) / a;
            if (t < t_min || t > t_max) {
                return false;
            }
        }
        record.t = t;
        record.point = ray_at(ray, record.t);
        const vec3 outward_normal = (record.point - sphere.center) / sphere.radius;
        hit_record_set_front_face(record, ray, outward_normal);
        record.material = sphere.material;
        return true;
    }
}

vec3 gradient_background(const in vec3 top_color,
                         const in vec3 bottom_color,
                         const in ray_t ray) {
    const float a = 0.5 * (ray.direction.y + 1.0);
    return a * top_color + (1.0 - a) * bottom_color;
}

bool near_zero(const in vec3 vector) {
    const float mu = 1e-8;
    return abs(vector.x) < mu &&
           abs(vector.y) < mu &&
           abs(vector.z) < mu;
}

// vec3 reflect(in vec3 v, 
//             in vec3 n) {
//    return v - 2 * dot(v, n) * n;
// }

// vec3 refract(in vec3 uv, 
//             in vec3 n, 
//             in float etai_over_etat) {
//    const float cos_theta = min(dot(-uv, n), 1.0);
//    const vec3 r_out_perp =  etai_over_etat * (uv + cos_theta*n);
//    const vec3 r_out_parallel = -sqrt(abs(1.0 - dot(r_out_perp, r_out_perp))) * n;
//    return r_out_perp + r_out_parallel;
// }

float reflectance(const in float cosine, 
                  const in float refraction_ratio) {
    // Use Schlick's approximation for reflectance.
    float r0 = (1 - refraction_ratio) / (1 + refraction_ratio);
    r0 = r0 * r0;
    return r0 + (1 - r0) * 
                (1 - cosine) *
                (1 - cosine) *
                (1 - cosine) *
                (1 - cosine) *
                (1 - cosine);
}

vec3 scatter(inout ray_t ray,
             const in hit_record_t hit_record) {
    switch (hit_record.material.type) {
        case MATERIAL_LAMBERTIAN:
            ray.origin = hit_record.point;
            ray.direction = normalize(hit_record.normal + 
                                      rand_vector_on_unit_sphere(hit_record.point.xy,
                                                                 hit_record.point.xz));
            if (near_zero(ray.direction)) {
                ray.direction = hit_record.normal;
            }
            return hit_record.material.albedo.xyz;
        case MATERIAL_METAL:
            ray.origin = hit_record.point;
            const vec3 reflected = reflect(ray.direction, hit_record.normal) + 
                                   hit_record.material.fuzz *
                                       rand_vector_on_unit_sphere(hit_record.point.xy,
                                                                 hit_record.point.xz);
            ray.direction = reflected;
            return hit_record.material.albedo.xyz;
        case MATERIAL_DIELECTRIC:
            ray.origin = hit_record.point;
            const float refraction_ratio = hit_record.front_face ? 
                1.0 / hit_record.material.refraction_index :
                hit_record.material.refraction_index;
            const float cos_theta = min(dot(-ray.direction, hit_record.normal), 1.0);
            const float sin_theta = sqrt(1 - cos_theta * cos_theta);
            if (refraction_ratio * sin_theta > 1.0 ||
                reflectance(cos_theta, refraction_ratio) > rand_01(hit_record.point.xy)) {
                ray.direction = reflect(ray.direction, hit_record.normal);
            } else {
                ray.direction = refract(ray.direction, 
                                        hit_record.normal, 
                                        refraction_ratio);
            }
            return vec3(1.0, 1.0, 1.0);
    }
}

vec3 brute_force_ray_tracing(in ray_t ray) {
    vec3 destination_color = vec3(0.0, 0.0, 0.0);
    vec3 accumulated_attenuation = vec3(1.0, 1.0, 1.0);
    #pragma optionNV (unroll all)
    for (int iter = 0; iter < RAY_BOUNCE_MAX_COUNT; ++ iter) {
        const float t_min = 0.001;
        float t_max = 1.0 / 0.0;
        hit_record_t hit_record = empty_hit_record();
        bool hit_anything = false;
        #pragma optionNV (unroll all)
        for (int sphere_idx = 0; sphere_idx < SPHERE_COUNT; ++ sphere_idx) {
            if (hit_sphere(spheres[sphere_idx], ray, t_min, t_max, hit_record)) {
                t_max = hit_record.t;
                hit_anything = true;
            }
        }
        if (hit_anything) {
            const vec3 attenuation = scatter(ray, hit_record);
            accumulated_attenuation = attenuation * accumulated_attenuation;
        } else {
            const vec3 sky_color = gradient_background(vec3(0.5, 0.7, 1.0), 
                                                       vec3(1.0, 1.0, 1.0),
                                                       ray);
            destination_color += accumulated_attenuation * sky_color;
            break;
        }
    }
    return destination_color;
}

vec3 ddd_dda_ray_tracing(in ray_t ray) {
    float world_t_min = 0.0001;
    float world_t_max = 1.0 / 0.0;
    if (!hit_aabb(world_aabb, ray, world_t_min, world_t_max)) {
        const vec3 sky_color = gradient_background(vec3(0.5, 0.7, 1.0), 
                                                   vec3(1.0, 1.0, 1.0),
                                                   ray);
        return sky_color;
    }
    vec3 destination_color = vec3(0.0, 0.0, 0.0);
    vec3 accumulated_attenuation = vec3(1.0, 1.0, 1.0);
    world_t_min += 1e-3;
    vec3 cur_pos = ray_at(ray, world_t_min);
    ivec3 cur_grid = world2grid(uniform_grid, cur_pos);
    ray.origin = cur_pos;
    #pragma optionNV (unroll all)
    for (int iter = 0; iter < RAY_BOUNCE_MAX_COUNT; ++ iter) {
        const vec3 inv_diretion = vec3(1.0) / ray.direction;
        const ivec3 ray_direction_positive = ivec3(ray.direction.x > 0.0, 
                                                   ray.direction.y > 0.0,
                                                   ray.direction.z > 0.0);
        const vec3 t_delta = uniform_grid.grid_size * inv_diretion;
        bool reach_boundary = false;
        bool hit_anything = false;
        hit_record_t hit_record = empty_hit_record();
        while (!reach_boundary && !hit_anything) {
            const uint grid_id = flatten_grid_coord(cur_grid);
            const float t_min = 0.01;
            float t_max = 1.0 / 0.0;
            for (uint sphere_idx_idx = geometry_in_grid_start[grid_id];
                 sphere_idx_idx < geometry_in_grid_start[grid_id + 1];
                 ++ sphere_idx_idx) {
                    const uint sphere_idx = intersection_sphere_id[sphere_idx_idx];
                    if (hit_sphere(spheres[sphere_idx], ray, t_min, t_max, hit_record)) {
                        t_max = hit_record.t;
                        hit_anything = true;
                    }
            }

            const ivec3 next_grid = cur_grid + ray_direction_positive;
            const vec3 t_proceed = (grid2world(uniform_grid, next_grid) - cur_pos) * inv_diretion;
            const ivec3 grid_step = 2 * ray_direction_positive - ivec3(1);
            const ivec3 grid_boundary = ray_direction_positive * ivec3(GRID_ALONG_X_AXIS,
                                                                       GRID_ALONG_Y_AXIS,
                                                                       GRID_ALONG_Z_AXIS) +
                                        (1 - ray_direction_positive) * ivec3(-1);
            const bool x_less_than_y = t_proceed.x < t_proceed.y;
            const bool x_less_than_z = t_proceed.x < t_proceed.z;
            const bool y_less_than_z = t_proceed.y < t_proceed.z;
            const bool x_smallest = x_less_than_y && x_less_than_z;
            const bool y_smallest = !x_less_than_y && y_less_than_z;
            const bool z_smallest = ( x_less_than_y && !x_less_than_z) ||
                                    (!x_less_than_y && !y_less_than_z);
            const int selected_field = 0 * int(x_smallest) + 1 * int(y_smallest) + 2 * int(z_smallest);
            cur_grid[selected_field] += grid_step[selected_field];
            reach_boundary = cur_grid[selected_field] == grid_boundary[selected_field];
            cur_pos = ray_at(ray, t_proceed[selected_field] + 1e-3);
            ray.origin = cur_pos;
        }

        if (hit_anything) {
            const vec3 attenuation = scatter(ray, hit_record);
            accumulated_attenuation = attenuation * accumulated_attenuation;
            cur_pos = hit_record.point;
            cur_grid = world2grid(uniform_grid, cur_pos);
        } else {
            const vec3 sky_color = gradient_background(vec3(0.5, 0.7, 1.0), 
                                                       vec3(1.0, 1.0, 1.0),
                                                       ray);
            destination_color += accumulated_attenuation * sky_color;
            break;
        }
    }
    return destination_color;
}
